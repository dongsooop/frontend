plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
}

def localProps = new Properties()
def localPropsFile = rootProject.file("local.properties")
if (localPropsFile.exists()) {
    localProps.load(new FileInputStream(localPropsFile))
}
def kakaoNativeAppKey = localProps.getProperty("KAKAO_NATIVE_APP_KEY", "")

def keystoreProperties = new Properties()
def keystorePropertiesFile = file("keystore/key.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    println "WARNING: key.properties not found. Release signing will fail until you add it."
}

android {
    namespace "com.dongsoop.site.dongsoop"
    compileSdkVersion flutter.compileSdkVersion

    defaultConfig {
        applicationId "com.dongsoop.site.dongsoop"
        minSdkVersion     flutter.minSdkVersion
        targetSdkVersion  flutter.targetSdkVersion
        versionCode       flutter.versionCode
        versionName       flutter.versionName

        manifestPlaceholders = [
                applicationName      : "android.app.Application",
                KAKAO_NATIVE_APP_KEY : kakaoNativeAppKey,
        ]

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86_64"
        }
    }

    signingConfigs {
        release {
            keyAlias      keystoreProperties['keyAlias']
            keyPassword   keystoreProperties['keyPassword']
            storeFile     keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            ndk {
                debugSymbolLevel 'SYMBOL_TABLE'
            }
        }
        debug { }
    }

    // JDK 17
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions { jvmTarget = "17" }
}

flutter { source = "../.." }

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.1.5"
    implementation platform("com.google.firebase:firebase-bom:34.4.0")
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-appcheck-playintegrity"
}